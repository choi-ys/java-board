plugins {
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'jacoco'
    id 'com.epages.restdocs-api-spec' version '0.11.5'
}

group = 'io.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    asciidoctorExtensions
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file('build/generated-snippets')
    querydslVersion = dependencyManagement.importedProperties['querydsl.version']
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    implementation "com.querydsl:querydsl-jpa" // querydsl-core를 querydsl-jpa가 포함
    implementation "com.querydsl:querydsl-collections"
    annotationProcessor "com.querydsl:querydsl-apt:${project.querydslVersion}:jpa" // querydsl JPAAnnotationProcessor 사용 지정
    annotationProcessor "jakarta.annotation:jakarta.annotation-api" // java.la ng.NoClassDefFoundError (javax.annotation.Generated) 발생 대응

    implementation 'com.auth0:java-jwt:3.16.0'
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.7.0'
    implementation 'it.ozimov:embedded-redis:0.7.2'

    compileOnly 'org.projectlombok:lombok'

    runtimeOnly 'com.h2database:h2'

    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.11.5'
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir
    finalizedBy 'jacocoTestReport'
}

/* [QueryDSL] Start */
def generated = 'src/main/generated'

// compile시 querydsl의 Qtype 객체 생성 위치 지정
tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// java source set 에 querydsl의 Qtype 객체 위치 추가
sourceSets {
    main.java.srcDirs += [ generated ]
}

// gradle clean 시, Qtype 디렉토리 삭제
clean {
    delete file(generated)
}
/* [QueryDSL] End */

/* [JaCoCo] Start */
jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        html.enabled true
        csv.enabled false
        xml.enabled false
    }

    // QueryDSL로 인해 생성되는 *QEntity를 Test Coverage 산정 리포트에서 제외
    def Qdomains = []
    for (qPattern in '**/QA'..'**/QZ') { // qPattern = '**/QA', '**/QB', ... '*.QZ'
        Qdomains.add(qPattern + '*')
    }

    // 여기부터
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            "**/*Application.class",
                            "**/config/redis/*",
                            "**/config/p6spy/*"
                    ] + Qdomains)
                })
        )
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

// https://www.baeldung.com/jacoco-report-exclude
jacocoTestCoverageVerification {
    // QueryDSL로 인해 생성되는 *QEntity를 Test Coverage 산정 시 제외
    def Qdomains = []
    for (qPattern in '*.QA'..'*.QZ') { // qPattern = '*.QA', '*.QB', ... '*.QZ'
        Qdomains.add(qPattern + '*')
    }

    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            excludes = [
                    "**/*Application.class",
                    "**/config/redis/*",
                    "**/config/p6spy/*"
            ] + Qdomains

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
            }
        }
    }
}
/* [JaCoCo] End */

/* [AsciiDoctor:Spring RETS Docs] Start */
asciidoctor {
    configurations 'asciidoctorExtensions'
    inputs.dir snippetsDir
    dependsOn test
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor

    from "${asciidoctor.outputDir}"
    into file("src/main/resources/static/docs")
}

build {
    dependsOn copyDocument
}
/* [AsciiDoctor:Spring RETS Docs] End */

/* [OAS with redoc] Start */
openapi3 {
    server = 'http://localhost:8084'
    title = 'USER-API'
    description = '사용자 리소스 API 입니다.'
    version = '0.1.0'
    format = 'yaml'
}
/* [OAS with redoc] End */

/**
 * Gradle build 최적화
 * - https://kangwoojin.github.io/programing/gradle-build-performace-2/#report-generation
 */
//// Parallel Testing Option : 이용 가능한 CPU의 반을 사용하여 TC 수행
//tasks.withType(Test) {
//    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
//}
//
//// Parallel Testing
//tasks.withType(Test) {
//    forkEvery = 100
//}

//// Report generationPermalink Options : TC 수행 결과 Report 생성 제외로 인한 성능 향상
//tasks.withType(Test) {
//    reports.html.enabled = false
//    reports.junitXml.enabled = false
//}
//
//// Compiler daemon Options
//tasks.withType(JavaCompile) {
//    options.fork = true
//}
